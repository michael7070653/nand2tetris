// This file is part of nand2tetris, as taught in The Hebrew University, and
// was written by Aviv Yaish. It is an extension to the specifications given
// [here](https://www.nand2tetris.org) (Shimon Schocken and Noam Nisan, 2017),
// as allowed by the Creative Common Attribution-NonCommercial-ShareAlike 3.0
// Unported [License](https://creativecommons.org/licenses/by-nc-sa/3.0/).

/**
 * Represents character strings. In addition for constructing and disposing
 * strings, the class features methods for getting and setting individual
 * characters of the string, for erasing the string's last character,
 * for appending a character to the string's end, and more typical
 * string-oriented operations.
 */
class String {
    /** constructs a new empty string with a maximum length of maxLength
     *  and initial length of 0. */
     field Array myStr;
     field int index, maxLength;

    constructor String new(int maxLen) {
        if (maxLen = 0){
            let maxLen = 1;
        }
        let myStr = Array.new(maxLen);
        let index = 0;
        let maxLength = maxLen;
        return this;
    }

    /** Disposes this string. */
    method void dispose() {
        do myStr.dispose();
        return;
    }

    /** Returns the current length of this string. */
    method int length() {
        return index;
    }

    /** Returns the character at the j-th location of this string. */
    method char charAt(int j) {
        return myStr[j];
    }

    /** Sets the character at the j-th location of this string to c. */
    method void setCharAt(int j, char c) {
        let myStr[j] = c;
        return;
    }

    /** Appends c to this string's end and returns this string. */
    method String appendChar(char c) {
        if (index < maxLength) {
            let myStr[index] = c;
            let index = index + 1;
        }
        return this;
    }

    /** Erases the last character from this string. */
    method void eraseLastChar() {
        if (index > 0) {
            let index = index - 1;
        }
        return;
    }

    /** Returns the integer value of this string, 
     *  until a non-digit character is detected. */
    method int intValue() {
        // Pseudocode:
        // 1. let val = 0
        // 2. for (i = 0 .. str.length()) do
        // 3.   let d = integer value of str.charAt(i)
        // 4.   let val = (val*10) + d
        // 5. return val
        var boolean isNegative;
        var int val, i;
        let val = 0;
        let i = 0;

    
        if (charAt(i) = 45) { // 45 represents the negative sign
            let isNegative = true;
            let i = 1;
        }
        while ((~(myStr[i] < 48) | ~(myStr[i] > 57)) & (i < index)) {
            let val = (val * 10) + (myStr[i] - 48);
            let i = i + 1;
        }
        if (isNegative) {
            return -val;
        }
        return val;
    }


    /** Sets this string to hold a representation of the given value. */
    method void setInt(int val) {
        // Pseudocode:
        // 1. let lastDigit = val % 10
        // 2. let c = character representing lastDigit
        // 3. if (val < 10)
        // 4.   return c (as a string)
        // 5. else
        // 6.   return int2String(val / 10).appendChar(c)
        var int div;
        let index = 0;
        let div = val;
        if (val < 0){
            do appendChar(45); // 45 represents the negative sign
            let div = -val; 
            }
        
        do helperSetInt(div);
        return;
    }

    method void helperSetInt(int val){
        var int lastDigit, mod, div;
        let  div = val / 10;
        let lastDigit = (val - (div * 10)) + 48;
        if(val  < 10){
            do appendChar(lastDigit);
            }
        else{
            do helperSetInt(div);
            do appendChar((lastDigit));
        }
        return;
    }



    /** Returns the new line character. */
    function char newLine() {
        return 128;
    }

    /** Returns the backspace character. */
    function char backSpace() {
        return 129;
    }

    /** Returns the double quote (") character. */
    function char doubleQuote() {
        return 34;
    }
}
